?start: or_term

?or_term: and_term
    | or_term "OR"i and_term

?and_term: not_term
    | and_term "AND"i not_term

?not_term: "!" not_term -> unary_not
    | comparison

?comparison: atom
    | atom num_operation atom -> numeric_comparison
    | atom cat_operation atom -> categorical_comparison

?atom: "(" or_term ")"
    | "[" CNAME "]" -> field_reference
    | SIGNED_NUMBER -> number
    | "'" SIGNED_NUMBER "'" -> category
    | "'" CNAME "'" -> text

!num_operation: "<" | "<=" | ">" | ">="
!cat_operation: "=" -> eq
    | "<>" -> ne

%import common.CNAME
%import common.SIGNED_NUMBER
%import common.WS
%ignore WS